let 
    __Today = DateTime.LocalNow(),
    __StartDate = #date(Date.Year(__Today)-1,1,1),
    __EndDate  = #date(Date.Year(__Today),12,31),
    P_Language = "EN", // Supporting Portuguese = PT
    P_FirstDayOfWeek = 1, // 1 = Monday
    P_DateFormat = "en-US", // pt-PT . Helps in date convert errors and client machine/data model language
    DayCount = Duration.Days(Duration.From(__EndDate - __StartDate)) + 1,
    Source = List.Dates(__StartDate,DayCount,#duration(1,0,0,0)),
    TableFromList = Table.FromList(Source, Splitter.SplitByNothing()),    
    ChangedType = Table.TransformColumnTypes(TableFromList,{{"Column1", type date}}),
    RenamedColumns = Table.RenameColumns(ChangedType,{{"Column1", "Date"}}),
    AddYear = Table.AddColumn(RenamedColumns, "Year", each Date.Year([Date])),
    AddQuarter = Table.AddColumn(AddYear, "Quarter", each Date.QuarterOfYear([Date])),
    AddSemester = Table.AddColumn(AddQuarter, "Semester", each if [Quarter] < 3 then 1 else 2),
    AddMonth = Table.AddColumn(AddSemester, "Month", each Date.Month([Date])),
    AddDay = Table.AddColumn(AddMonth, "Day", each Date.Day([Date])),    
    AddMonthName = Table.AddColumn(AddDay, "Month (Name)", each Date.ToText([Date], "MMMM", P_DateFormat), type text),
    AddShortMonthName = Table.AddColumn(AddMonthName, "Month (Short Name)", each try(Text.Range([#"Month (Name)"],0,3)) otherwise [#"Month (Name)"]),
    AddCalendarMonth = Table.AddColumn(AddShortMonthName, "Month of Year", each [#"Month (Short Name)"] & " " & Number.ToText([Year])),
    AddCalendarQtr = Table.AddColumn(AddCalendarMonth, "Quarter of Year", each (if P_Language = "PT" then "T" else "Q") & Number.ToText([Quarter]) & " " & Number.ToText([Year])),    
    AddCalendarSem = Table.AddColumn(AddCalendarQtr, "Semester of Year", each "S" & Number.ToText([Semester]) & " " & Number.ToText([Year])),    
    AddWeek = Table.AddColumn(AddCalendarSem, "Week", each Date.WeekOfYear([Date], P_FirstDayOfWeek )),
    AddCalendarWeek = Table.AddColumn(AddWeek, "Week of Year", each (if P_Language = "PT" then "S" else "W") & Number.ToText([Week]) & " " & Number.ToText([Year])),
    AddDayWeek = Table.AddColumn(AddCalendarWeek , "Week Day", each Date.DayOfWeek([Date], P_FirstDayOfWeek ) + 1),
    AddDayName = Table.AddColumn(AddDayWeek, "Week Day (Name)", each Date.ToText([Date], "dddd", P_DateFormat), type text),
    AddMonthYear = Table.AddColumn(AddDayName, "MonthYearId", each [Year] *100 + [Month]),
    AddWeekYear = Table.AddColumn(AddMonthYear, "WeekYearId", each [Year] * 100 + [Week]),
    AddStartWeek = Table.AddColumn(AddWeekYear , "Start of Week", each Date.StartOfWeek([Date], P_FirstDayOfWeek), type date),
    AddEndWeek = Table.AddColumn(AddStartWeek , "End of Week", each Date.EndOfWeek([Date], P_FirstDayOfWeek), type date),
    AddQuarterYear = Table.AddColumn(AddEndWeek, "QuarterYearId", each [Year] * 100 + [Quarter]),
    AddSemesterYear = Table.AddColumn(AddQuarterYear, "SemesterYearId", each [Year] * 100 + [Semester]),
    #"Capitalized Each Word" = Table.TransformColumns(AddSemesterYear,{{"Month (Name)", Text.Proper}, {"Month of Year", Text.Proper}, {"Week Day (Name)", Text.Proper}}),
    #"Period (Year)" = Table.AddColumn(#"Capitalized Each Word", "Period (Year)", each [Year] - Date.Year(__Today)),
    #"Period (Month)" = Table.AddColumn(#"Period (Year)", "Period (Month)", each [#"Period (Year)"] * 12 + ([Month] - Date.Month(__Today))),
    #"Period (Day)" = Table.AddColumn(#"Period (Month)", "Period (Day) - Num", each Duration.TotalDays([Date] - DateTime.Date(__Today))),
    #"Period (Day) - Short" = Table.AddColumn(#"Period (Day)", "Period (Day)", each if [#"Period (Day) - Num"] > 10 then "> 10 " else if [#"Period (Day) - Num"] < -10 then "< -10" else [#"Period (Day) - Num"] ),
    #"Period (Day) - Short - Order" = Table.AddColumn(#"Period (Day) - Short", "Period (Day) - Order", each if [#"Period (Day) - Num"] > 10 then "11" else if [#"Period (Day) - Num"] < -10 then "-11" else [#"Period (Day) - Num"] ),
    #"Removed Columns" = Table.RemoveColumns(#"Period (Day) - Short - Order",{"Start of Week", "End of Week"}),
    #"Changed Type" = Table.TransformColumnTypes(#"Removed Columns",{{"Year", Int64.Type}, {"Quarter", Int64.Type}, {"Day", Int64.Type}, {"Month", Int64.Type}, {"Date", type date}, {"Month (Name)", type text}, {"Month (Short Name)", type text}, {"Month of Year", type text}, {"Quarter of Year", type text}, {"Semester of Year", type text}, {"Week", Int64.Type}, {"Week of Year", type text}, {"Week Day", Int64.Type}, {"Week Day (Name)", type text}, {"MonthYearId", Int64.Type}, {"WeekYearId", Int64.Type}, {"QuarterYearId", Int64.Type}, {"SemesterYearId", Int64.Type}, {"Period (Year)", Int64.Type}, {"Period (Month)", Int64.Type}, {"Period (Day)", type text}, {"Period (Day) - Order", Int64.Type}, {"Semester", Int64.Type}, {"Period (Day) - Num", Int64.Type}}),
    #"Removed Columns1" = Table.RemoveColumns(#"Changed Type",{"Period (Day) - Num"}),
    #"Add WeekDay Name" = Table.AddColumn(#"Removed Columns1", "Week Day (Short)", each Text.Start([#"Week Day (Name)"], 3), type text),
    #"If Portuguese" = Table.RenameColumns(#"Add WeekDay Name",{{"Year", "Ano"},{"Semester", "Semestre"}, {"Quarter", "Trimestre"}, {"Month", "Mês (Num.)"}, {"Month (Name)", "Mês"}, {"Month of Year", "Mês (Ano)"}, {"Quarter of Year", "Trimestre (Ano)"}, {"Semester of Year", "Semestre (Ano)"}, {"MonthYearId", "CodMes(Ano)"}, {"Period (Year)", "Período (Ano)"}, {"Period (Month)", "Período (Mês)"}, {"Date", "Data"}, {"Day", "Dia"}, {"Week", "Semana"}, {"Week of Year", "Semana (Ano)"}, {"Week Day", "Dia Semana (Num.)"}, {"Week Day (Name)", "Dia Semana"}, {"Month (Short Name)", "Mês (Abrev.)"}, {"WeekYearId", "CodSemanaAno"}, {"QuarterYearId", "CodTrimestreAno"}, {"SemesterYearId", "CodSemestreAno"}, {"Period (Day)", "Período (Dia)"}, {"Period (Day) - Order", "Período (Dia) - Ordem"},{"Week Day (Short)", "Dia da Semana (Abrev.)"}}),
    result = if P_Language = "PT" then #"If Portuguese" else #"Add WeekDay Name"
in
    result
